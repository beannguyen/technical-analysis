#
# BTFG_STFG_Strat
#
# This script adapted from posts from @kerberos007
# https://twitter.com/kerberos007
# 
# Want the latest version of this script?
# https://github.com/korygill/technical-analysis#
# Use on thinkorswim and thinkscript
# author @korygill
#

script GetBollingerBandPercent
{
    input price = close;
    input upper = 2;
    input lower = 2;
    input averageType = AverageType.SIMPLE;
    input displace = 0;
    input length = 20;

    def upperBand = BollingerBands(price, displace, length, lower, upper, averageType).UpperBand;
    def lowerBand = BollingerBands(price, displace, length, lower, upper, averageType).LowerBand;

    plot BBPercent = (price - lowerBand) / (upperBand - lowerBand) * 100;
}

input averageType = AverageType.SIMPLE;
input price = close;
input displace = 0;
input length = 20;
input StdDev_DnBuy = -2.0;
input StdDev_UpBuy = 2.0;
input StdDev_DnSell = -1.0;
input StdDev_UpSell = 1.0;
input LongShortBoth = {Long, Short, default Both};

def PercentBBuy = GetBollingerBandPercent(price, StdDev_UpBuy, StdDev_DnBuy);

def PercentBSell = GetBollingerBandPercent(price, StdDev_UpSell, StdDev_DnSell);

def ZeroLine = 0;
def HalfLine = 50;
def UnitLine = 100;

def lsb;
switch (LongShortBoth)
{
    case Long: lsb = 0;
    case Short: lsb = 1;
    default: lsb = 2;
}

# LONG
AddOrder(OrderType.BUY_TO_OPEN, 
    Crosses(PercentBBuy, ZeroLine, CrossingDirection.ABOVE) and lsb != 1,
    tickColor = COLOR.WHITE, arrowColor = COLOR.GREEN
    );

AddOrder(OrderType.SELL_TO_CLOSE, 
    Crosses(PercentBSell, UnitLine, CrossingDirection.ABOVE) and lsb != 1,
    tickColor = COLOR.WHITE, arrowColor = COLOR.RED
    );

# SHORT
AddOrder(OrderType.SELL_TO_OPEN, 
    Crosses(PercentBBuy, UnitLine, CrossingDirection.BELOW) and lsb != 0,
    tickColor = COLOR.WHITE, arrowColor = COLOR.YELLOW
    );

AddOrder(OrderType.BUY_TO_CLOSE, 
    Crosses(PercentBSell, ZeroLine, CrossingDirection.BELOW) and lsb != 0,
    tickColor = COLOR.WHITE, arrowColor = COLOR.MAGENTA
    );

AddLabel(yes, "Go Long", COLOR.GREEN);
AddLabel(yes, "Cover Long", COLOR.RED);
AddLabel(yes, "Go Short", COLOR.YELLOW);
AddLabel(yes, "Cover Short", COLOR.MAGENTA);
